buildscript {
    ext.kotlin_version = '1.3.50'
    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.5.0'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id 'de.undercouch.download'
}

group 'com.example.flutter_olm'
version '1.0-SNAPSHOT'

rootProject.allprojects {
    repositories {
        google()
        jcenter()
    }
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'de.undercouch.download'

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation fileTree(dir: 'libs', include: ['*.jar'])
}

task downloadOlm(type: Download) {
    src 'https://gitlab.matrix.org/matrix-org/olm/-/archive/master/olm-master.zip'
    dest 'olm.zip'
}

task installOlm(dependsOn: downloadOlm, type: Copy) {
    from zipTree(downloadOlm.dest).matching {
        include '*/CMakeLists.txt'
        include '*/cmake/**'
        include '*/lib/**'
        include '*/include/**'
        include '*/src/**'
        eachFile { fcd ->
            fcd.relativePath = new RelativePath(true, fcd.relativePath.segments.drop(1))
        }
    }
    into 'olm/'
}

android {
    compileSdkVersion 28

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
    defaultConfig {
        minSdkVersion 16

        externalNativeBuild {
            cmake {
                arguments "-DOLM_TESTS=NO"
            }
        }
    }
    lintOptions {
        disable 'InvalidPackage'
    }

    preBuild.dependsOn(installOlm)

    externalNativeBuild {
        cmake {
            path "olm/CMakeLists.txt"
        }
    }
}

